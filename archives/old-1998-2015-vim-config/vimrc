" Modeline and Notes {{{
" vim: fdm=marker ts=2 sts=2 sw=2 fmr={{{,}}}

" vim configuration file
" (c) 1998-2015 Joerg Kuetemeier <jk@kuetemeier.net>

" Licensed under the Apache License, Version 2.0 (the "License");
" you may not use this file except in compliance with the License.
" You may obtain a copy of the License at
"
"     http://www.apache.org/licenses/LICENSE-2.0
"
" Unless required by applicable law or agreed to in writing, software
" distributed under the License is distributed on an "AS IS" BASIS,
" WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
" See the License for the specific language governing permissions and
" limitations under the License.

" }}}

" detect OS {{{
  let s:is_windows = has('win16') || has('win32') || has('win64')
  let s:is_cygwin = has('win32unix')
  let s:is_macvim = has('gui_macvim')
  let s:is_osx = has('macunix')
  let s:is_linux = has('unix') && !has('macunix') && !has('win32unix')
"}}}

" dotvim settings {{{
  if !exists('g:dotvim_settings') || !exists('g:dotvim_settings.version')
    echom 'The g:dotvim_settings and g:dotvim_settings.version variables must be defined.  Please consult the README.'
    finish
  endif

  let s:cache_dir = get(g:dotvim_settings, 'cache_dir', '~/.vim/.cache')

  if g:dotvim_settings.version != 2
    echom 'The version number in your shim does not match the distribution version.  Please consult the README changelog section.'
    finish
  endif

  " initialize default settings
  let s:settings = {}
  let s:settings.default_indent = 2
  let s:settings.max_column = 80
  let s:settings.autocomplete_method = 'ycm'
  let s:settings.enable_cursorcolumn = 0
  let s:settings.colorscheme = 'hybrid'

  " check for a 'minimal server version' configuration
  if !exists('g:dotvim_settings.minimal_server_version')
    let s:settings.msv = 0
  else
    let s:settings.msv = g:dotvim_settings.minimal_server_version
  endif

  if s:settings.msv
    " use neocomplcache on server config as default
    let s:settings.autocomplete_method = 'neocomplcache'
  endif

  " if has('lua')
  "   let s:settings.autocomplete_method = 'neocomplete'
  " elseif filereadable(expand("~/.vim/bundle/YouCompleteMe/python/ycm_core.*"))
  "   let s:settings.autocomplete_method = 'ycm'
  " endif
  if exists('g:dotvim_settings.autocomplete')
    let s:settings.autocomplete_method = g:dotvim_settings.autocomplete
  endif

  " default: show custom foldtext
  if !exists('g:dotvim_settings.no_own_foldtext')
    let s:settings.no_own_foldtext = 0
  else
    let s:settings.no_own_foldtext = g:dotvim_settings.no_own_foldtext
  endif

  " default: install only the default colorscheme
  if !exists('g:dotvim_settings.install_all_colorschemes')
    let s:settings.all_colorschemes = 0
  else
    let s:settings.all_colorschemes = g:dotvim_settings.install_all_colorschemes
  endif

  if exists('g:dotvim_settings.plugin_groups')
    let s:settings.plugin_groups = g:dotvim_settings.plugin_groups
  else
    let s:settings.plugin_groups = []
    call add(s:settings.plugin_groups, 'core')
    call add(s:settings.plugin_groups, 'editing')
    call add(s:settings.plugin_groups, 'folding')
    call add(s:settings.plugin_groups, 'germanmapping')
    call add(s:settings.plugin_groups, 'scm')
    if !s:settings.msv
      call add(s:settings.plugin_groups, 'web')
      call add(s:settings.plugin_groups, 'javascript')
      call add(s:settings.plugin_groups, 'ruby')
      call add(s:settings.plugin_groups, 'python')
      call add(s:settings.plugin_groups, 'scala')
      call add(s:settings.plugin_groups, 'go')
      call add(s:settings.plugin_groups, 'indent-guides')
      call add(s:settings.plugin_groups, 'navigation')
      call add(s:settings.plugin_groups, 'unite')
      call add(s:settings.plugin_groups, 'autocomplete')
      call add(s:settings.plugin_groups, 'textobj')
      call add(s:settings.plugin_groups, 'misc')
      call add(s:settings.plugin_groups, 'numbers')
      call add(s:settings.plugin_groups, 'spell')
      if s:is_windows
        call add(s:settings.plugin_groups, 'windows')
      endif
    endif

    " exclude all language-specific plugins by default
    if !exists('g:dotvim_settings.plugin_groups_exclude')
        let g:dotvim_settings.plugin_groups_exclude = ['web','javascript','ruby','python','go','scala']
    endif
    for group in g:dotvim_settings.plugin_groups_exclude
      let i = index(s:settings.plugin_groups, group)
      if i != -1
        call remove(s:settings.plugin_groups, i)
      endif
    endfor

    if exists('g:dotvim_settings.plugin_groups_include')
      for group in g:dotvim_settings.plugin_groups_include
        call add(s:settings.plugin_groups, group)
      endfor
    endif
  endif

  " override defaults with the ones specified in g:dotvim_settings
  for key in keys(s:settings)
    if has_key(g:dotvim_settings, key)
      let s:settings[key] = g:dotvim_settings[key]
    endif
  endfor
"}}}

" setup & neobundle {{{
  set nocompatible
  set all& "reset everything to their defaults
  if s:is_windows
    set rtp+=~/.vim
  endif
  set rtp+=~/.vim/bundle/neobundle.vim
  call neobundle#begin(expand('~/.vim/bundle/'))
  NeoBundleFetch 'Shougo/neobundle.vim'
"}}}

" functions {{{
  function! s:get_cache_dir(suffix) "{{{
    return resolve(expand(s:cache_dir . '/' . a:suffix))
  endfunction "}}}
  function! Source(begin, end) "{{{
    let lines = getline(a:begin, a:end)
    for line in lines
      execute line
    endfor
  endfunction "}}}
  function! Preserve(command) "{{{
    " preparation: save last search, and cursor position.
    let _s=@/
    let l = line(".")
    let c = col(".")
    " do the business:
    execute a:command
    " clean up: restore previous search history, and cursor position
    let @/=_s
    call cursor(l, c)
  endfunction "}}}
  function! StripTrailingWhitespace() "{{{
    call Preserve("%s/\\s\\+$//e")
  endfunction "}}}
  function! EnsureExists(path) "{{{
    if !isdirectory(expand(a:path))
      call mkdir(expand(a:path))
    endif
  endfunction "}}}
  function! CloseWindowOrKillBuffer() "{{{
    let number_of_windows_to_this_buffer = len(filter(range(1, winnr('$')), "winbufnr(v:val) == bufnr('%')"))

    " never bdelete a nerd tree
    if matchstr(expand("%"), 'NERD') == 'NERD'
      wincmd c
      return
    endif

    if number_of_windows_to_this_buffer > 1
      wincmd c
    else
      bdelete
    endif
  endfunction "}}}

  " Delete buffer while keeping window layout (don't close buffer's windows) {{{
  " Version 2008-11-18 from http://vim.wikia.com/wiki/VimTip165
  if v:version < 700 || exists('loaded_bclose') || &cp
    finish
  endif
  let loaded_bclose = 1
  if !exists('bclose_multiple')
    let bclose_multiple = 1
  endif

  " Display an error message.
  function! s:Warn(msg)
    echohl ErrorMsg
    echomsg a:msg
    echohl NONE
  endfunction

  " Command ':Bclose' executes ':bd' to delete buffer in current window.
  " The window will show the alternate buffer (Ctrl-^) if it exists,
  " or the previous buffer (:bp), or a blank buffer if no previous.
  " Command ':Bclose!' is the same, but executes ':bd!' (discard changes).
  " An optional argument can specify which buffer to close (name or number).
  function! s:Bclose(bang, buffer)
    if empty(a:buffer)
      let btarget = bufnr('%')
    elseif a:buffer =~ '^\d\+$'
      let btarget = bufnr(str2nr(a:buffer))
    else
      let btarget = bufnr(a:buffer)
    endif
    if btarget < 0
      call s:Warn('No matching buffer for '.a:buffer)
      return
    endif
    if empty(a:bang) && getbufvar(btarget, '&modified')
      call s:Warn('No write since last change for buffer '.btarget.' (use :Bclose!)')
      return
    endif
    " Numbers of windows that view target buffer which we will delete.
    let wnums = filter(range(1, winnr('$')), 'winbufnr(v:val) == btarget')
    if !g:bclose_multiple && len(wnums) > 1
      call s:Warn('Buffer is in multiple windows (use ":let bclose_multiple=1")')
      return
    endif
    let wcurrent = winnr()
    for w in wnums
      execute w.'wincmd w'
      let prevbuf = bufnr('#')
      if prevbuf > 0 && buflisted(prevbuf) && prevbuf != w
        buffer #
      else
        bprevious
      endif
      if btarget == bufnr('%')
        " Numbers of listed buffers which are not the target to be deleted.
        let blisted = filter(range(1, bufnr('$')), 'buflisted(v:val) && v:val != btarget')
        " Listed, not target, and not displayed.
        let bhidden = filter(copy(blisted), 'bufwinnr(v:val) < 0')
        " Take the first buffer, if any (could be more intelligent).
        let bjump = (bhidden + blisted + [-1])[0]
        if bjump > 0
          execute 'buffer '.bjump
        else
          "execute 'enew'.a:bang
          Startify
        endif
      endif
    endfor
    execute 'bdelete'.a:bang.' '.btarget
    execute wcurrent.'wincmd w'
  endfunction
  command! -bang -complete=buffer -nargs=? Bclose call s:Bclose('<bang>', '<args>')
  "}}}
"}}}

" Read sensible common vim settings {{{
  " https://github.com/tpope/vim-sensible
  if filereadable(expand("~/.vim/sensible.vim"))
    source ~/.vim/sensible.vim
  endif
" }}}

" base configuration {{{
  set timeoutlen=1000                                 "mapping timeout
  set ttimeoutlen=50                                  "keycode timeout

  set mouse=a                                         "enable mouse
  set mousehide                                       "hide when characters are typed
  set history=1000                                    "number of command lines to remember
  set ttyfast                                         "assume fast terminal connection
  set viewoptions=folds,options,cursor,unix,slash     "unix/windows compatibility
  set encoding=utf-8                                  "set encoding for text
  if exists('$TMUX')
    set clipboard=
  else
    if has('unnamedplus')  " When possible use + register for copy-paste
      set clipboard=unnamed,unnamedplus
    else         " On mac and Windows, use * register for copy-paste
      set clipboard=unnamed                           "sync with OS clipboard
    endif
  endif
  set hidden                                          "allow buffer switching without saving
  set autoread                                        "auto reload if file saved externally
  set fileformats+=mac                                "add mac to auto-detection of file format line endings
  set nrformats-=octal                                "always assume decimal numbers
  set showcmd
  set tags=tags;/
  set showfulltag
  set modeline
  set modelines=5

  if s:is_windows && !s:is_cygwin
    " ensure correct shell in gvim
    set shell=c:\windows\system32\cmd.exe
  endif

  if $SHELL =~ '/fish$'
    " VIM expects to be run from a POSIX shell.
    set shell=sh
  endif

  set noshelltemp                                     "use pipes

  " whitespace
  set backspace=indent,eol,start                      "allow backspacing everything in insert mode
  set autoindent                                      "automatically indent to match adjacent lines
  set expandtab                                       "spaces instead of tabs
  set smarttab                                        "use shiftwidth to enter tabs
  let &tabstop=s:settings.default_indent              "number of spaces per tab for display
  let &softtabstop=s:settings.default_indent          "number of spaces per tab in insert mode
  let &shiftwidth=s:settings.default_indent           "number of spaces when indenting
  set list                                            "highlight whitespace
  set listchars=tab:»·,trail:·,extends:❯,precedes:❮
  set shiftround
  set linebreak
  let &showbreak='↪ '

  set scrolloff=1                                     "always show content after scroll
  set scrolljump=1                                    "minimum number of lines to scroll
  set display+=lastline
  set wildmenu                                        "show list for autocomplete
  set wildmode=list:full
  set wildignorecase

  " Open new split panes to right and bottom, which feels more natural
  set splitbelow
  set splitright

  " disable sounds
  set noerrorbells
  set novisualbell
  set t_vb=

  " searching
  set hlsearch                                        "highlight searches
  set incsearch                                       "incremental searching
  set ignorecase                                      "ignore case for searching
  set smartcase                                       "do case-sensitive if there's a capital letter
  if executable('ack')
    set grepprg=ack\ --nogroup\ --column\ --smart-case\ --nocolor\ --follow\ $*
    set grepformat=%f:%l:%c:%m
  endif
  if executable('ag')
    set grepprg=ag\ --nogroup\ --column\ --smart-case\ --nocolor\ --follow
    set grepformat=%f:%l:%c:%m
  endif

  " vim file/folder management {{{
    " persistent undo
    if exists('+undofile')
      set undofile
      let &undodir = s:get_cache_dir('undo')
    endif

    " backups
    set nobackup
    set nowritebackup
    let &backupdir = s:get_cache_dir('backup')

    " swap files
    let &directory = s:get_cache_dir('swap')
    set noswapfile

    call EnsureExists(s:cache_dir)
    call EnsureExists(&undodir)
    call EnsureExists(&backupdir)
    call EnsureExists(&directory)
  "}}}

  let mapleader = ","
  let g:mapleader = ","
"}}}

" ui configuration {{{
  set showmatch                                       "automatically highlight matching braces/brackets/etc.
  set matchtime=2                                     "tens of a second to show matching parentheses
  set number
  set lazyredraw
  set laststatus=2
  set noshowmode

  set cursorline
  autocmd WinLeave * setlocal nocursorline
  autocmd WinEnter * setlocal cursorline
  let &colorcolumn=s:settings.max_column
  if s:settings.enable_cursorcolumn
    set cursorcolumn
    autocmd WinLeave * setlocal nocursorcolumn
    autocmd WinEnter * setlocal cursorcolumn
  endif

  if has('conceal')
    set conceallevel=1
    set listchars+=conceal:Δ
  endif

  if has('gui_running')
    if s:is_windows
      autocmd GUIEnter * simalt ~x
    endif

    set guioptions=tc

    " set guioptions+=t  "tear off menu items
    " set guioptions-=T  "toolbar icons
    " set guioptions-=rR " dispable right toolbar
    " set guioptions-=lL " dispable left toolbar
    " set guioptions-=bB " dispable bottom toolbar
    " set guioptions+=c  " use console dialogs instead of poup dialogs

    if s:is_macvim
      silent! set guifont=Inconsolata-g\ for\ Powerline:h13
      if &guifont != 'Inconsolata-g for Powerline:h13'
        set guifont=Ubuntu_Mono:h16
      endif
      " set guifont=Inconsolata-g\ for\ Powerline:h13
      set transparency=0
    endif

    if s:is_windows
      set gfn=Ubuntu_Mono:h10
    endif

    if has('gui_gtk')
      set gfn=Ubuntu\ Mono\ 11
    endif

    " open maximized
    " set lines=999 columns=9999
    " maximize mvim
    nnoremap <silent> <Leader>U :set lines=999 columns=9999<cr>
    nnoremap <silent> <Leader>u :set lines=25 columns=80<cr>
  else

    nnoremap <silent> <Leader>U :echo "This is not a GUI Vim, cannot resize"<cr>
    nnoremap <silent> <Leader>u :echo "This is not a GUI Vim, cannot resize"<cr>

    if $COLORTERM == 'gnome-terminal'
      set t_Co=256 "why you no tell me correct colors?!?!
    endif
    if $TERM_PROGRAM == 'iTerm.app'
      " different cursors for insert vs normal mode
      if exists('$TMUX')
        let &t_SI = "\<Esc>Ptmux;\<Esc>\<Esc>]50;CursorShape=1\x7\<Esc>\\"
        let &t_EI = "\<Esc>Ptmux;\<Esc>\<Esc>]50;CursorShape=0\x7\<Esc>\\"
      else
        let &t_SI = "\<Esc>]50;CursorShape=1\x7"
        let &t_EI = "\<Esc>]50;CursorShape=0\x7"
      endif
    endif
  endif
"}}}

" plugin/mapping configuration {{{
  if count(s:settings.plugin_groups, 'core') "{{{
    " allows you to configure % to match more than just single characters
    NeoBundle 'matchit.zip'

    " lean & mean status/tabline for vim that's light as air
    NeoBundle 'bling/vim-airline' "{{{
      let g:airline#extensions#tabline#enabled = 1
      let g:airline#extensions#tabline#left_sep=' '
      let g:airline#extensions#tabline#left_alt_sep='¦'

      set noshowmode

      " find themes at: https://github.com/bling/vim-airline/wiki/Screenshots
      let g:airline_theme='hybrid' " works best for hybrid theme
      let g:airline_enable_branch=1
      let g:airline_powerline_fonts=1
      let g:airline_detect_whitespace = 1
      let g:airline#extensions#hunks#non_zero_only = 1

      let g:airline#extensions#tabline#enabled = 2
      " let g:airline#extensions#tabline#fnamemod = ':t'
      let g:airline#extensions#tabline#buffer_min_count = 2
    "}}}

    " surround.vim: quoting/parenthesizing made simple
    NeoBundle 'tpope/vim-surround'

    " repeat.vim: enable repeating supported plugin maps with '.'
    NeoBundle 'tpope/vim-repeat'

    " eunuch.vim: helpers for UNIX
    NeoBundle 'tpope/vim-eunuch'

    " unimpaired.vim: pairs of handy bracket mappings
    NeoBundle 'tpope/vim-unimpaired' "{{{
      nmap <c-up> [e
      nmap <c-down> ]e
      vmap <c-up> [egv
      vmap <c-down> ]egv
    "}}}

    if !s:settings.msv
      " dispatch.vim: asynchronous build and test dispatcher
      NeoBundle 'tpope/vim-dispatch'

      " Interactive command execution in Vim.
      NeoBundle 'Shougo/vimproc.vim', {
        \ 'build': {
        \ 'mac': 'make -f make_mac.mak',
        \ 'unix': 'make -f make_unix.mak',
        \ 'cygwin': 'make -f make_cygwin.mak',
        \ 'windows': '"C:\Program Files (x86)\Microsoft Visual Studio 11.0\VC\bin\nmake.exe" make_msvc32.mak',
        \ },
      \ }
    endif
  endif "}}}
  if count(s:settings.plugin_groups, 'knet') "{{{
    " Kuetemeier.NET GmbH plugin with custom snippets
    NeoBundle 'git@git.kuetemeier.net:kuetemeiernet/knet-vim-snippets.git'
  endif "}}}
  if count(s:settings.plugin_groups, 'jk') "{{{
    " Personal plugin from Jörg Kütemeier
    NeoBundle 'git@git.kuetemeier.net:jkuetemeier/jk-vim-plugin.git'
  endif "}}}
  if count(s:settings.plugin_groups, 'web') "{{{
    NeoBundleLazy 'groenewege/vim-less', {'autoload':{'filetypes':['less']}}
    NeoBundleLazy 'cakebaker/scss-syntax.vim', {'autoload':{'filetypes':['scss','sass']}}
    NeoBundleLazy 'hail2u/vim-css3-syntax', {'autoload':{'filetypes':['css','scss','sass']}}
    NeoBundleLazy 'ap/vim-css-color', {'autoload':{'filetypes':['css','scss','sass','less','styl']}}
    NeoBundleLazy 'othree/html5.vim', {'autoload':{'filetypes':['html']}}
    NeoBundleLazy 'wavded/vim-stylus', {'autoload':{'filetypes':['styl']}}
    NeoBundleLazy 'digitaltoad/vim-jade', {'autoload':{'filetypes':['jade']}}
    NeoBundleLazy 'juvenn/mustache.vim', {'autoload':{'filetypes':['mustache']}}
    NeoBundleLazy 'gregsexton/MatchTag', {'autoload':{'filetypes':['html','xml']}}
    " NeoBundleLazy 'mattn/emmet-vim', {'autoload':{'filetypes':['html','xml','xsl','xslt','xsd','css','sass','scss','less','mustache']}} "{{{
    "   function! s:zen_html_tab()
    "     let line = getline('.')
    "     if match(line, '<.*>') < 0
    "       return "\<c-y>,"
    "     endif
    "     return "\<c-y>n"
    "   endfunction
    "   autocmd FileType xml,xsl,xslt,xsd,css,sass,scss,less,mustache imap <buffer><tab> <c-y>,
    "   autocmd FileType html imap <buffer><expr><tab> <sid>zen_html_tab()
    " "}}}
  endif "}}}
  if count(s:settings.plugin_groups, 'javascript') "{{{
    NeoBundleLazy 'marijnh/tern_for_vim', {
      \ 'autoload': { 'filetypes': ['javascript'] },
      \ 'build': {
        \ 'mac': 'npm install',
        \ 'unix': 'npm install',
        \ 'cygwin': 'npm install',
        \ 'windows': 'npm install',
      \ },
    \ }
    autocmd FileType javascript setlocal omnifunc=tern#Complete
    NeoBundleLazy 'pangloss/vim-javascript', {'autoload':{'filetypes':['javascript']}}
    NeoBundleLazy 'maksimr/vim-jsbeautify', {'autoload':{'filetypes':['javascript']}} "{{{
      nnoremap <Leader>fjs :call JsBeautify()<cr>
    "}}}
    NeoBundleLazy 'leafgarland/typescript-vim', {'autoload':{'filetypes':['typescript']}}
    NeoBundleLazy 'kchmck/vim-coffee-script', {'autoload':{'filetypes':['coffee']}}
    NeoBundleLazy 'mmalecki/vim-node.js', {'autoload':{'filetypes':['javascript']}}
    NeoBundleLazy 'leshill/vim-json', {'autoload':{'filetypes':['javascript','json']}}
    NeoBundleLazy 'othree/javascript-libraries-syntax.vim', {'autoload':{'filetypes':['javascript','coffee','ls','typescript']}}
  endif "}}}
  if count(s:settings.plugin_groups, 'ruby') "{{{
    NeoBundle 'tpope/vim-rails'
    NeoBundle 'tpope/vim-bundler'
  endif "}}}
  if count(s:settings.plugin_groups, 'python') "{{{
    NeoBundleLazy 'klen/python-mode', {'autoload':{'filetypes':['python']}} "{{{
      let g:pymode_rope=0
    "}}}
    NeoBundleLazy 'davidhalter/jedi-vim', {'autoload':{'filetypes':['python']}} "{{{
      let g:jedi#popup_on_dot=0
    "}}}
  endif "}}}
  if count(s:settings.plugin_groups, 'scala') "{{{
    NeoBundle 'derekwyatt/vim-scala'
    NeoBundle 'megaannum/vimside'
  endif "}}}
  if count(s:settings.plugin_groups, 'go') "{{{
    NeoBundleLazy 'fatih/vim-go', {'autoload':{'filetypes':['go']}}
    " recommended, run :GoInstallBinaries"

    augroup golang
      au BufRead,BufNewFile *.tmpl set filetype=gotplhtml
      " let g:go_snippet_engine = "neosnippet"

      " use gofmt compatible tab settings
      autocmd FileType go setlocal shiftwidth=4 tabstop=4 softtabstop=4 noexpandtab

      " Use real tabs in golang files and hide tab listchars
      autocmd FileType go setlocal list listchars=tab:\ \ ,trail:·,extends:❯,precedes:❮

      " get all the errors from syntastic
      autocmd FileType go let g:syntastic_aggregate_errors = 1
      autocmd FileType go let g:syntastic_go_checkers = ["go", "govet", "golint"]

      " mappings for vim-go
      autocmd FileType go nmap <leader>d <plug>(go-doc)
      autocmd FileType go nmap <leader>r <plug>(go-run)
      autocmd FileType go nmap <leader>b <plug>(go-build)
      autocmd FileType go nmap <leader>t <plug>(go-test)
      autocmd FileType go nmap <leader>ds <plug>(go-def-split)
      autocmd FileType go nmap <leader>dv <plug>(go-def-vertical)
      autocmd FileType go nmap <leader>dt <plug>(go-def-tab)
      autocmd FileType go nmap gd <plug>(go-def-split)
    augroup END

    " format with goimports instead of gofmt
    let g:go_fmt_command = "goimports"

  endif "}}}
  if count(s:settings.plugin_groups, 'scm') "{{{
    NeoBundle 'mhinz/vim-signify' "{{{
      let g:signify_update_on_bufenter=0
    "}}}
    if executable('hg')
      NeoBundle 'bitbucket:ludovicchabant/vim-lawrencium'
    endif
    NeoBundle 'tpope/vim-fugitive' "{{{
      nnoremap <silent> <Leader>gs :Gstatus<CR>
      nnoremap <silent> <Leader>gd :Gdiff<CR>
      nnoremap <silent> <Leader>gc :Gcommit<CR>
      nnoremap <silent> <Leader>gb :Gblame<CR>
      nnoremap <silent> <Leader>gl :Glog<CR>
      nnoremap <silent> <Leader>gp :Git push<CR>
      nnoremap <silent> <Leader>gw :Gwrite<CR>
      nnoremap <silent> <Leader>gr :Gremove<CR>
      autocmd BufReadPost fugitive://* set bufhidden=delete
    "}}}
    NeoBundleLazy 'gregsexton/gitv', {'depends':['tpope/vim-fugitive'], 'autoload':{'commands':'Gitv'}} "{{{
      nnoremap <silent> <Leader>gv :Gitv<CR>
      nnoremap <silent> <Leader>gV :Gitv!<CR>
    "}}}
  endif "}}}
  if count(s:settings.plugin_groups, 'autocomplete') "{{{
    NeoBundle 'honza/vim-snippets'
    if s:settings.autocomplete_method == 'ycm' "{{{
      " YouCompleteMe {{{
      NeoBundle 'Valloric/YouCompleteMe', {
            \ 'build' : {
            \     'mac' : './install.sh --clang-completer',
            \     'unix' : './install.sh --clang-completer',
            \     'windows' : './install.sh --clang-completer --system-libclang --omnisharp-completer',
            \     'cygwin' : './install.sh --clang-completer --system-libclang --omnisharp-completer'
            \    }
            \ }
      " Add or remove arguments to install.sh as necessary.
      " Additional steps might be necessary for Windows, as always. ;)
        let g:ycm_complete_in_comments_and_strings=1
        let g:ycm_key_list_select_completion=['<C-n>', '<Down>', '<tab>']
        let g:ycm_key_list_previous_completion=['<C-p>', '<Up>', '<s-tab>']
        let g:ycm_filetype_blacklist={'unite': 1}
      "}}}
      NeoBundle 'SirVer/ultisnips' "{{{
         let g:UltiSnipsExpandTrigger="<Leader>m"
         let g:UltiSnipsJumpForwardTrigger="<Leader>m"
         let g:UltiSnipsJumpBackwardTrigger="<Leader>."
         let g:UltiSnipsSnippetDirectories = ["ultisnips"]
         let g:UltiSnipsSnippetsDir='~/.vim/ultisnips'
      "}}}
    else
      NeoBundle 'Shougo/neosnippet-snippets'
      NeoBundle 'Shougo/neosnippet.vim' "{{{
        let g:neosnippet#snippets_directory='~/.vim/bundle/vim-snippets/snippets,~/.vim/snippets'
        let g:neosnippet#enable_snipmate_compatibility=1

        imap <expr><TAB> neosnippet#expandable_or_jumpable() ? "\<Plug>(neosnippet_expand_or_jump)" : (pumvisible() ? "\<C-n>" : "\<TAB>")
        smap <expr><TAB> neosnippet#expandable_or_jumpable() ? "\<Plug>(neosnippet_expand_or_jump)" : "\<TAB>"
        imap <expr><S-TAB> pumvisible() ? "\<C-p>" : ""
        smap <expr><S-TAB> pumvisible() ? "\<C-p>" : ""
      "}}}
    endif "}}}
    if s:settings.autocomplete_method == 'neocomplete' "{{{
      NeoBundleLazy 'Shougo/neocomplete.vim', {'autoload':{'insert':1}, 'vim_version':'7.3.885'} "{{{
        let g:neocomplete#enable_at_startup=1
        let g:neocomplete#data_directory=s:get_cache_dir('neocomplete')
      "}}}
      " <TAB>: completion.
      inoremap <expr><TAB>  pumvisible() ? "\<C-n>" : "\<TAB>"
      " <C-h>, <BS>: close popup and delete backword char.
      inoremap <expr><C-h> neocomplete#smart_close_popup()."\<C-h>"
      inoremap <expr><BS> neocomplete#smart_close_popup()."\<C-h>"
      inoremap <expr><C-y>  neocomplete#close_popup()
      inoremap <expr><C-e>  neocomplete#cancel_popup()
      " Enable omni completion.
      autocmd FileType css setlocal omnifunc=csscomplete#CompleteCSS
      autocmd FileType html,markdown setlocal omnifunc=htmlcomplete#CompleteTags
      autocmd FileType javascript setlocal omnifunc=javascriptcomplete#CompleteJS
      autocmd FileType python setlocal omnifunc=pythoncomplete#Complete
      autocmd FileType xml setlocal omnifunc=xmlcomplete#CompleteTags

      " Enable heavy omni completion.
      if !exists('g:neocomplete#sources#omni#input_patterns')
        let g:neocomplete#sources#omni#input_patterns = {}
      endif
    endif "}}}
    if s:settings.autocomplete_method == 'neocomplcache' "{{{
      NeoBundleLazy 'Shougo/neocomplcache.vim', {'autoload':{'insert':1}} "{{{
        let g:neocomplcache_enable_at_startup=1
        let g:neocomplcache_temporary_dir=s:get_cache_dir('neocomplcache')
        let g:neocomplcache_enable_fuzzy_completion=1
      "}}}
    endif "}}}
  endif "}}}
  if count(s:settings.plugin_groups, 'editing') "{{{
    " An extensible & universal comment vim-plugin that also handles embedded
    " filetypes
    NeoBundle 'tomtom/tcomment_vim'

    " True Sublime Text style multiple selections for Vim
    NeoBundle 'terryma/vim-multiple-cursors'

    " The missing motion for Vim
    NeoBundle 'justinmk/vim-sneak' "{{{
      let g:sneak#streak = 1
    "}}}

    " Scratch Buffer
    NeoBundle 'mtth/scratch.vim'

    " Vim script for text filtering and alignment
    NeoBundleLazy 'godlygeek/tabular', {'autoload':{'commands':'Tabularize'}} "{{{
      nmap <Leader>a& :Tabularize /&<CR>
      vmap <Leader>a& :Tabularize /&<CR>
      nmap <Leader>a= :Tabularize /=<CR>
      vmap <Leader>a= :Tabularize /=<CR>
      nmap <Leader>a: :Tabularize /:<CR>
      vmap <Leader>a: :Tabularize /:<CR>
      nmap <Leader>a:: :Tabularize /:\zs<CR>
      vmap <Leader>a:: :Tabularize /:\zs<CR>
      nmap <Leader>a, :Tabularize /,<CR>
      vmap <Leader>a, :Tabularize /,<CR>
      nmap <Leader>a<Bar> :Tabularize /<Bar><CR>
      vmap <Leader>a<Bar> :Tabularize /<Bar><CR>
    "}}}

    " EditorConfig plugin for Vim
    NeoBundleLazy 'editorconfig/editorconfig-vim', {'autoload':{'insert':1}}

    " endwise.vim: wisely add 'end' in ruby, endfunction/endif/more in vim
    " script, etc
    NeoBundle 'tpope/vim-endwise'

    " speeddating.vim: use CTRL-A/CTRL-X to increment dates, times, and more
    NeoBundle 'tpope/vim-speeddating'

    " Vim plugin that allows you to visually select increasingly larger
    " regions of text using the same key combination.
    " use '+' and '_' to expand and shrink regions
    NeoBundle 'terryma/vim-expand-region'

    " A Narrow Region Plugin for vim (like Emacs Narrow Region)
    NeoBundle 'chrisbra/NrrwRgn'
    " edit actual selected visual region
    map <Leader>n :NR<CR>
    " mark a visual selected region as NrrwRgn
    map <Leader>nm :NRP<CR>
    " edit all marked regions
    map <Leader>nn :NRM<CR>

    " Vim plugin, insert or delete brackets, parens, quotes in pair
    NeoBundle 'jiangmiao/auto-pairs'

    if !s:settings.msv
      " Zen Editing
      NeoBundle 'junegunn/goyo.vim'
      NeoBundle 'amix/vim-zenroom2' "{{{
        nnoremap <silent> <Leader>o :Goyo<cr>
      "}}}
    endif

    map <silent> w <Plug>CamelCaseMotion_w
    map <silent> b <Plug>CamelCaseMotion_b
    map <silent> e <Plug>CamelCaseMotion_e
    omap <silent> iw <Plug>CamelCaseMotion_iw
    xmap <silent> iw <Plug>CamelCaseMotion_iw
    omap <silent> ib <Plug>CamelCaseMotion_ib
    xmap <silent> ib <Plug>CamelCaseMotion_ib
    omap <silent> ie <Plug>CamelCaseMotion_ie
    xmap <silent> ie <Plug>CamelCaseMotion_ie
    sunmap w
    sunmap b
    sunmap e
    " carefull... overwrites <Leader>w (without mappings above)
    NeoBundle 'vim-scripts/camelcasemotion'
  endif "}}}
  if count(s:settings.plugin_groups, 'navigation') "{{{
    NeoBundle 'mileszs/ack.vim' "{{{
      if executable('ag')
        let g:ackprg = "ag --nogroup --column --smart-case --follow"
      endif
    "}}}
    NeoBundleLazy 'mbbill/undotree', {'autoload':{'commands':'UndotreeToggle'}} "{{{
      let g:undotree_SplitLocation='botright'
      let g:undotree_SetFocusWhenToggle=1
      nnoremap <silent> <F5> :UndotreeToggle<CR>
    "}}}
    NeoBundleLazy 'EasyGrep', {'autoload':{'commands':'GrepOptions'}} "{{{
      let g:EasyGrepRecursive=1
      let g:EasyGrepAllOptionsInExplorer=1
      let g:EasyGrepCommand=1
      nnoremap <Leader>vo :GrepOptions<cr>
    "}}}
    NeoBundle 'kien/ctrlp.vim', { 'depends': 'tacahiroy/ctrlp-funky' } "{{{
      let g:ctrlp_clear_cache_on_exit=1
      let g:ctrlp_max_height=40
      let g:ctrlp_show_hidden=0
      let g:ctrlp_follow_symlinks=1
      let g:ctrlp_max_files=20000
      let g:ctrlp_cache_dir=s:get_cache_dir('ctrlp')
      let g:ctrlp_reuse_window='startify'
      let g:ctrlp_extensions=['funky']
      let g:ctrlp_custom_ignore = {
            \ 'dir': '\v[\/]\.(git|hg|svn|idea)$',
            \ 'file': '\v\.DS_Store$'
            \ }

      if executable('ag')
        let g:ctrlp_user_command='ag %s -l --nocolor -g ""'
      endif

      nmap \ [ctrlp]
      nnoremap [ctrlp] <nop>

      nnoremap [ctrlp]t :CtrlPBufTag<cr>
      nnoremap [ctrlp]T :CtrlPTag<cr>
      nnoremap [ctrlp]l :CtrlPLine<cr>
      nnoremap [ctrlp]o :CtrlPFunky<cr>
      nnoremap [ctrlp]b :CtrlPBuffer<cr>
    "}}}
    NeoBundleLazy 'scrooloose/nerdtree', {'autoload':{'commands':['NERDTreeToggle','NERDTreeFind']}} "{{{
      let NERDTreeShowHidden=1
      let NERDTreeQuitOnOpen=0
      let NERDTreeShowLineNumbers=1
      let NERDTreeChDirMode=0
      let NERDTreeShowBookmarks=1
      let NERDTreeIgnore=['\.git','\.hg']
      let NERDTreeBookmarksFile=s:get_cache_dir('NERDTreeBookmarks')
      nnoremap <F9> :NERDTreeToggle<CR>
      nnoremap <F8> :NERDTreeFind<CR>
    "}}}
    NeoBundleLazy 'majutsushi/tagbar', {'autoload':{'commands':'TagbarToggle'}} "{{{
      nnoremap <silent> <F10> :TagbarToggle<CR>
    "}}}
    NeoBundle 'Lokaltog/vim-easymotion'
  endif "}}}
  if count(s:settings.plugin_groups, 'unite') "{{{
    NeoBundle 'Shougo/unite.vim' "{{{
      let bundle = neobundle#get('unite.vim')
      function! bundle.hooks.on_source(bundle)
        call unite#filters#matcher_default#use(['matcher_fuzzy'])
        call unite#filters#sorter_default#use(['sorter_rank'])
        call unite#custom#source('line,outline','matchers','matcher_fuzzy')
        call unite#custom#profile('default', 'context', {
              \ 'start_insert': 1,
              \ 'direction': 'botright',
              \ })
      endfunction

      let g:unite_data_directory=s:get_cache_dir('unite')
      let g:unite_source_history_yank_enable=1
      let g:unite_source_rec_max_cache_files=5000

      if executable('ag')
        let g:unite_source_grep_command='ag'
        let g:unite_source_grep_default_opts='--nocolor --line-numbers --nogroup -S -C4'
        let g:unite_source_grep_recursive_opt=''
      elseif executable('ack')
        let g:unite_source_grep_command='ack'
        let g:unite_source_grep_default_opts='--no-heading --no-color -C4'
        let g:unite_source_grep_recursive_opt=''
      endif

      function! s:unite_settings()
        nmap <buffer> Q <plug>(unite_exit)
        nmap <buffer> <esc> <plug>(unite_exit)
        imap <buffer> <esc> <plug>(unite_exit)
      endfunction
      autocmd FileType unite call s:unite_settings()

      nmap <space> [unite]
      "nmap <Leader>m [unite]
      nnoremap [unite] <nop>

      if s:is_windows
        nnoremap <silent> [unite]<space> :<C-u>Unite -toggle -auto-resize -buffer-name=mixed file_rec:! buffer file_mru bookmark<cr><c-u>
        nnoremap <silent> [unite]f :<C-u>Unite -toggle -auto-resize -buffer-name=files file_rec:!<cr><c-u>
      else
        nnoremap <silent> [unite]<space> :<C-u>Unite -toggle -auto-resize -buffer-name=mixed file_rec/async:! buffer file_mru bookmark<cr><c-u>
        nnoremap <silent> [unite]f :<C-u>Unite -toggle -auto-resize -buffer-name=files file_rec/async:!<cr><c-u>
      endif
      nnoremap <silent> [unite]e :<C-u>Unite -buffer-name=recent file_mru<cr>
      nnoremap <silent> [unite]y :<C-u>Unite -buffer-name=yanks history/yank<cr>
      nnoremap <silent> [unite]l :<C-u>Unite -auto-resize -buffer-name=line line<cr>
      nnoremap <silent> [unite]b :<C-u>Unite -auto-resize -buffer-name=buffers buffer<cr>
      nnoremap <silent> [unite]/ :<C-u>Unite -no-quit -buffer-name=search grep:.<cr>
      nnoremap <silent> [unite]m :<C-u>Unite -auto-resize -buffer-name=mappings mapping<cr>
      nnoremap <silent> [unite]s :<C-u>Unite -quick-match buffer<cr>
    "}}}
    NeoBundleLazy 'Shougo/neomru.vim', {'autoload':{'unite_sources':'file_mru'}}
    NeoBundleLazy 'osyo-manga/unite-airline_themes', {'autoload':{'unite_sources':'airline_themes'}} "{{{
      nnoremap <silent> [unite]a :<C-u>Unite -winheight=10 -auto-preview -buffer-name=airline_themes airline_themes<cr>
    "}}}
    NeoBundleLazy 'ujihisa/unite-colorscheme', {'autoload':{'unite_sources':'colorscheme'}} "{{{
      nnoremap <silent> [unite]c :<C-u>Unite -winheight=10 -auto-preview -buffer-name=colorschemes colorscheme<cr>
    "}}}
    NeoBundleLazy 'tsukkee/unite-tag', {'autoload':{'unite_sources':['tag','tag/file']}} "{{{
      nnoremap <silent> [unite]t :<C-u>Unite -auto-resize -buffer-name=tag tag tag/file<cr>
    "}}}
    NeoBundleLazy 'Shougo/unite-outline', {'autoload':{'unite_sources':'outline'}} "{{{
      nnoremap <silent> [unite]o :<C-u>Unite -auto-resize -buffer-name=outline outline<cr>
    "}}}
    NeoBundleLazy 'Shougo/unite-help', {'autoload':{'unite_sources':'help'}} "{{{
      nnoremap <silent> [unite]h :<C-u>Unite -auto-resize -buffer-name=help help<cr>
    "}}}
    NeoBundleLazy 'Shougo/junkfile.vim', {'autoload':{'commands':'JunkfileOpen','unite_sources':['junkfile','junkfile/new']}} "{{{
      let g:junkfile#directory=s:get_cache_dir('junk')
      nnoremap <silent> [unite]j :<C-u>Unite -auto-resize -buffer-name=junk junkfile junkfile/new<cr>
    "}}}
  endif "}}}
  if count(s:settings.plugin_groups, 'indent-guides') "{{{
    NeoBundle 'nathanaelkane/vim-indent-guides' "{{{
      let g:indent_guides_start_level=1
      let g:indent_guides_guide_size=1
      let g:indent_guides_enable_on_vim_startup=0
      let g:indent_guides_color_change_percent=3
      if !has('gui_running')
        let g:indent_guides_auto_colors=0
        function! s:indent_set_console_colors()
          hi IndentGuidesOdd ctermbg=235
          hi IndentGuidesEven ctermbg=236
        endfunction
        autocmd VimEnter,Colorscheme * call s:indent_set_console_colors()
      endif
    "}}}
  endif "}}}
  if count(s:settings.plugin_groups, 'spell') "{{{
    " Activate spell checking {{{
    if has("spell")
      " turn spelling on by default
      " set spell

      " toggle spelling with F4 key
      map <F4> :set spell!<CR><Bar>:echo "Spell Check: " . strpart("OffOn", 3 * &spell, 3)<CR>

      " they were using white on white
      highlight PmenuSel ctermfg=black ctermbg=lightgray

      " limit it to just the top 10 items
      set sps=best,10
      set spelllang=en,de
    endif
    " }}}

    " Spellfile location {{{
    " Set spellfile to location that is guaranteed to exist, can be symlinked to
    " Dropbox or kept in Git and managed outside of thoughtbot/dotfiles using rcm.
    set spellfile=$HOME/.vim/spell/en.utf-8.add
    " }}}
  endif "}}}
  if count(s:settings.plugin_groups, 'germanmapping') "{{{
    " Remapping of german keys {{{
    map <silent> ü [
    map! <silent> ü [
    map <silent> ö :
    map! <silent> ö :
    map <silent> Ö \|
    map! <silent> Ö \|
    map <silent> ä ]
    map! <silent> ä ]
    map <silent> Ü {
    map! <silent> Ü {
    map <silent> Ä }
    map! <silent> Ä }
    " }}}
    "set keymap=german

    " Key combinations to genereate german umlauts {{{
    inoremap <silent> o" ö
    inoremap <silent> a" ä
    inoremap <silent> u" ü
    inoremap <silent> O" Ö
    inoremap <silent> A" Ä
    inoremap <silent> U" Ü
    " }}}
  endif "}}}

  if count(s:settings.plugin_groups, 'folding') "{{{
    if has('folding')

      " fold on the marker
      set foldmethod=syntax

      " turn on folding
      set foldenable

      " A column with the specified width is shown at the side of the window
      " which indicates open and closed folds.
      set foldcolumn=0

      " autofold anything (but I can still fold manually).
      "  set foldlevel=0

      " Specify the movements which open folds.
      set foldopen=block,hor,mark,percent,quickfix,tag,search

      "open all folds by default
      set foldlevelstart=99

      "enable xml folding
      let g:xml_syntax_folding=1

      if !s:settings.no_own_foldtext
        function! NeatFoldText() "{{{
          " let line = ' ' . substitute(getline(v:foldstart), '^\s*"\?\s*\|\s*"\?\s*{{' . '{\d*\s*', '', 'g') . ' '
          let line = ' ' . substitute(getline(v:foldstart), '^\s*"\?\s*\|\s*"\?\s*{' . '\d*\s*', '', 'g') . ' '
          let lines_count = v:foldend - v:foldstart + 1
          let lines_count_text = '| ' . printf("%10s", lines_count . ' lines') . ' |'
          let foldchar = matchstr(&fillchars, 'fold:\zs.')
          let foldtextstart = strpart('' . repeat(foldchar, v:foldlevel*2) . line, 0, (winwidth(0)*2)/3)
          let foldtextend = lines_count_text . repeat(foldchar, 8)
          let foldtextlength = strlen(substitute(foldtextstart . foldtextend, '.', 'x', 'g')) + &foldcolumn
          return foldtextstart . repeat(foldchar, winwidth(0)-foldtextlength) . foldtextend
        endfunction
        "}}}

        set foldtext=NeatFoldText()
      endif

      " set direct fold levels {{{
      nmap <Leader>f0 :set foldlevel=0<CR>:set foldcolumn=0<CR>:
        \ echo "Foldlevel ="&foldlevel<cr>

      nmap <Leader>f1 :set foldlevel=1<CR>:set foldcolumn=1<CR>:
        \ echo "Foldlevel ="&foldlevel<cr>

      nmap <Leader>f2 :set foldlevel=2<CR>:set foldcolumn=3<CR>:
        \ echo "Foldlevel ="&foldlevel<cr>

      nmap <Leader>f3 :set foldlevel=3<CR>:set foldcolumn=4<CR>:
        \ echo "Foldlevel ="&foldlevel<cr>

      nmap <Leader>f4 :set foldlevel=4<CR>:set foldcolumn=5<CR>:
        \ echo "Foldlevel ="&foldlevel<cr>

      nmap <Leader>f5 :set foldlevel=5<CR>:set foldcolumn=5<CR>:
        \ echo "Foldlevel ="&foldlevel<cr>

      nmap <Leader>f6 :set foldlevel=6<CR>:set foldcolumn=5<CR>:
        \ echo "Foldlevel ="&foldlevel<cr>

      nmap <Leader>f7 :set foldlevel=7<CR>:set foldcolumn=5<CR>:
        \ echo "Foldlevel ="&foldlevel<cr>

      nmap <Leader>f8 :set foldlevel=8<CR>:set foldcolumn=5<CR>:
        \ echo "Foldlevel ="&foldlevel<cr>

      nmap <Leader>f9 :set foldlevel=9<CR>:set foldcolumn=5<CR>:
        \ echo "Foldlevel ="&foldlevel<cr>
      "}}}

      " folds with info message {{{
      nnoremap zr zr:echo "Foldlevel ="&foldlevel<cr>
      nnoremap zm zm:echo "Foldlevel ="&foldlevel<cr>
      nnoremap zR zR:echo "Foldlevel ="&foldlevel<cr>
      nnoremap zM zM:echo "Foldlevel ="&foldlevel<cr>
      " }}}
    endif
  endif "}}}
  if count(s:settings.plugin_groups, 'numbers') "{{{
    NeoBundle 'myusuf3/numbers.vim'
    let g:numbers_exclude = ['unite', 'tagbar', 'startify', 'gundo', 'vimshell',
          \ 'w3m', 'tagbar', 'gundo', 'nerdtree', 'Vundle']
  endif "}}}
  if count(s:settings.plugin_groups, 'textobj') "{{{
    " NeoBundle 'kana/vim-textobj-user'
    " NeoBundle 'kana/vim-textobj-indent'
    " NeoBundle 'kana/vim-textobj-entire'
    " NeoBundle 'lucapette/vim-textobj-underscore'
    NeoBundle 'michaeljsmith/vim-indent-object'
  endif "}}}
  if count(s:settings.plugin_groups, 'misc') "{{{
    if exists('$TMUX')
      NeoBundle 'christoomey/vim-tmux-navigator'
    endif
    NeoBundle 'kana/vim-vspec'
    NeoBundleLazy 'tpope/vim-scriptease', {'autoload':{'filetypes':['vim']}}
    NeoBundleLazy 'tpope/vim-markdown', {'autoload':{'filetypes':['markdown']}}
    if executable('redcarpet') && executable('instant-markdown-d')
      NeoBundleLazy 'suan/vim-instant-markdown', {'autoload':{'filetypes':['markdown']}}
    endif
    NeoBundleLazy 'guns/xterm-color-table.vim', {'autoload':{'commands':'XtermColorTable'}}
    NeoBundle 'chrisbra/vim_faq'
    " NeoBundle 'vimwiki'
    NeoBundle 'bufkill.vim'
    NeoBundle 'mhinz/vim-startify' "{{{
      let g:startify_session_dir = s:get_cache_dir('sessions')
      let g:startify_change_to_vcs_root = 1
      let g:startify_show_sessions = 1
      nnoremap <F1> :Startify<cr>
    "}}}
    NeoBundle 'scrooloose/syntastic' "{{{
      let g:syntastic_error_symbol = '✗'
      let g:syntastic_style_error_symbol = '✠'
      let g:syntastic_warning_symbol = '∆'
      let g:syntastic_style_warning_symbol = '≈'
    "}}}
    NeoBundleLazy 'mattn/gist-vim', { 'depends': 'mattn/webapi-vim', 'autoload': { 'commands': 'Gist' } } "{{{
      let g:gist_post_private=1
      let g:gist_show_privates=1
    "}}}
    NeoBundleLazy 'Shougo/vimshell.vim', {'autoload':{'commands':[ 'VimShell', 'VimShellInteractive' ]}} "{{{
      if s:is_macvim
        let g:vimshell_editor_command='mvim'
      else
        let g:vimshell_editor_command='vim'
      endif
      let g:vimshell_right_prompt='getcwd()'
      let g:vimshell_data_directory=s:get_cache_dir('vimshell')
      let g:vimshell_vimshrc_path='~/.vim/vimshrc'

      nnoremap <Leader>c :VimShell -split<cr>
      nnoremap <Leader>cc :VimShell -split<cr>
      nnoremap <Leader>cn :VimShellInteractive node<cr>
      nnoremap <Leader>cl :VimShellInteractive lua<cr>
      nnoremap <Leader>cr :VimShellInteractive irb<cr>
      nnoremap <Leader>cp :VimShellInteractive python<cr>
    "}}}
    NeoBundleLazy 'zhaocai/GoldenView.Vim', {'autoload':{'mappings':['<Plug>ToggleGoldenViewAutoResize']}} "{{{
      let g:goldenview__enable_default_mapping=0
      nmap <F3> <Plug>ToggleGoldenViewAutoResize
    "}}}
  endif "}}}
  if count(s:settings.plugin_groups, 'windows') "{{{
    NeoBundleLazy 'PProvost/vim-ps1', {'autoload':{'filetypes':['ps1']}} "{{{
      autocmd BufNewFile,BufRead *.ps1,*.psd1,*.psm1 setlocal ft=ps1
    "}}}
    NeoBundleLazy 'nosami/Omnisharp', {'autoload':{'filetypes':['cs']}}
  endif "}}}

  nnoremap <Leader>nbu :Unite neobundle/update -vertical -no-start-insert<cr>
"}}}

" mappings {{{
  " formatting shortcuts
  nmap <Leader>fef :call Preserve("normal gg=G")<CR>
  nmap <Leader>f$ :call StripTrailingWhitespace()<CR>
  nmap <Leader>ft :%s/\s\+$//<CR>
  vmap <Leader>s :sort<cr>

  " eval vimscript by line or visual selection
  nmap <silent> <Leader>e :call Source(line('.'), line('.'))<CR>
  vmap <silent> <Leader>e :call Source(line('v'), line('.'))<CR>

  nnoremap <Leader>w :w<cr>

  " toggle paste
  map <F2> :set invpaste<CR>:set paste?<CR>
  imap <F2> :set invpaste<CR>:set paste?<CR>
  nnoremap <Leader>p :pu<CR>

  " remap arrow keys
  "nnoremap <left> :bprev<CR>
  "nnoremap <right> :bnext<CR>
  "nnoremap <up> :tabnext<CR>
  "nnoremap <down> :tabprev<CR>

  " smash escape
  inoremap jj <esc>
  inoremap kj <esc>

  " change cursor position in insert mode
  " inoremap <C-h> <left>
  " inoremap <C-l> <right>

  inoremap <C-u> <C-g>u<C-u>

  if mapcheck('<space>/') == ''
    nnoremap <space>/ :vimgrep //gj **/*<left><left><left><left><left><left><left><left>
  endif

  " sane regex {{{
  "  nnoremap / /\v
  "  vnoremap / /\v
  "  nnoremap ? ?\v
  "  vnoremap ? ?\v
  "  nnoremap :s/ :s/\v
  " }}}

  " command-line window {{{
    nnoremap q: q:i
    nnoremap q/ q/i
    nnoremap q? q?i
  " }}}

  " screen line scroll
  nnoremap <silent> j gj
  nnoremap <silent> k gk

  " auto center {{{
    nnoremap <silent> n nzz
    nnoremap <silent> N Nzz
    nnoremap <silent> * *zz
    nnoremap <silent> # #zz
    nnoremap <silent> g* g*zz
    nnoremap <silent> g# g#zz
    nnoremap <silent> <C-o> <C-o>zz
    nnoremap <silent> <C-i> <C-i>zz
  "}}}

  " reselect visual block after indent
  vnoremap < <gv
  vnoremap > >gv

  " reselect last paste
  nnoremap <expr> gp '`[' . strpart(getregtype(), 0, 1) . '`]'

  " find current word in quickfix
  nnoremap <Leader>fw :execute "vimgrep ".expand("<cword>")." %"<cr>:copen<cr>
  " find last search in quickfix
  nnoremap <Leader>ff :execute 'vimgrep /'.@/.'/g %'<cr>:copen<cr>

  " shortcuts for windows {{{
    nnoremap <Leader>v <C-w>v<C-w>l
    nnoremap <Leader>s <C-w>s
    nnoremap <Leader>vsa :vert sba<cr>
    nnoremap <C-h> <C-w>h
    nnoremap <C-j> <C-w>j
    nnoremap <C-k> <C-w>k
    nnoremap <C-l> <C-w>l

    inoremap <C-h> <ESC><C-w>ha
    inoremap <C-j> <ESC><C-w>ja
    inoremap <C-k> <ESC><C-w>ka
    inoremap <C-l> <ESC><C-w>la

    nmap <Leader>j :b#<CR>
    imap <Leader>j <ESC>:b#<CR>
    nmap <Leader>l :bnext<CR>
    nmap <Leader>h :bprev<CR>
    imap <Leader>l <ESC>:bnext<CR>
    imap <Leader>h <ESC>:bprev<CR>
  "}}}

  " tab shortcuts
  map <Leader>tn :tabnew<CR>
  map <Leader>tc :tabclose<CR>

  " make Y consistent with C and D. See :help Y.
  nnoremap Y y$

  " hide annoying quit message
  nnoremap <C-c> <C-c>:echo<cr>

  " window killer
  nnoremap <silent> Q :call CloseWindowOrKillBuffer()<cr>

  " quick buffer open
  nnoremap gb :ls<cr>:e #

  if neobundle#is_sourced('vim-dispatch')
    nnoremap <Leader>tag :Dispatch ctags -R<cr>
  endif

  " general
  "nmap <Leader>l :set list! list?<cr>
  nnoremap <BS> :set hlsearch! hlsearch?<cr>

  map <F11> :echo "hi<" . synIDattr(synID(line("."),col("."),1),"name") . '> trans<'
        \ . synIDattr(synID(line("."),col("."),0),"name") . "> lo<"
        \ . synIDattr(synIDtrans(synID(line("."),col("."),1)),"name") . ">"<CR>

  " helpers for profiling {{{
    nnoremap <silent> <Leader>DD :exe ":profile start profile.log"<cr>:exe ":profile func *"<cr>:exe ":profile file *"<cr>
    nnoremap <silent> <Leader>DP :exe ":profile pause"<cr>
    nnoremap <silent> <Leader>DC :exe ":profile continue"<cr>
    nnoremap <silent> <Leader>DQ :exe ":profile pause"<cr>:noautocmd qall!<cr>
  "}}}

  " Movement {{{

    " have the h and l cursor keys wrap between lines (like <Space> and <BkSpc>
    " do by default), and ~ covert case over line breaks; also have the cursor
    " keys wrap in insert mode:
    set whichwrap=h,l,~,[,]

    " use <Ctrl>+N/<Ctrl>+P to cycle through files:
    nnoremap <C-N> :next<CR>
    nnoremap <C-P> :prev<CR>

  " }}}

  " Easier formatting of paragraphs {{{
    vmap Q gq
    " have Q reformat the current paragraph (or selected text if there is any):
    vnoremap Q gqqp
    vnoremap Q gq
  " }}}

  " unhighlight search highlight with C-L
    nnoremap <silent> <C-l> :nohlsearch<CR><C-l><C-w>l

  " Resize windows {{{
  " You can use the command :resize +5 or :res -5 to resize windows
  " this are just quick shortcuts
    nnoremap <silent> <Leader>+ :exe "resize " . (winheight(0) * 3/2)<CR>
    nnoremap <silent> <Leader>- :exe "resize " . (winheight(0) * 2/3)<CR>
  "}}}

  "map <Leader>q <Plug>BufKillBd
  nnoremap <silent> <Leader>bd :Bclose<CR>
  nnoremap <silent> <Leader>q :Bclose<CR>

"}}}

" commands {{{
  command! -bang Q q<bang>
  command! -bang QA qa<bang>
  command! -bang Qa qa<bang>
"}}}

" autocmd {{{
  augroup myac
    " go back to previous position of cursor if any
    autocmd BufReadPost *
      \ if line("'\"") > 0 && line("'\"") <= line("$") |
      \  exe 'normal! g`"zvzz' |
      \ endif

    autocmd FileType c,cpp setlocal comments-=:// comments+=f://
    autocmd FileType css,scss nnoremap <silent> <Leader>S vi{:sort<CR>
    " }
    autocmd FileType css,scss setlocal foldmethod=marker foldmarker={,}
    autocmd FileType git,gitcommit setlocal foldmethod=syntax foldlevel=1
    autocmd FileType gitcommit setlocal spell
    autocmd FileType help setlocal ai fo+=2n | silent! setlocal nospell |
      \ nnoremap <silent><buffer> q :q<CR>
    autocmd FileType js,scss,css autocmd BufWritePre <buffer> call StripTrailingWhitespace()
    autocmd FileType markdown setlocal nolist
    autocmd FileType perl setlocal comments-=:# comments+=f:#
    autocmd FileType pl setlocal comments-=:# comments+=f:#
    autocmd FileType python setlocal foldmethod=indent
    autocmd FileType vim setlocal fdm=indent keywordprg=:help

    " disable automatic comments in the next line
    autocmd FileType * setlocal formatoptions-=c formatoptions-=r formatoptions-=o

  augroup END
"}}}

" Colorschemes {{{
  NeoBundle 'w0ng/vim-hybrid'
  if s:settings.all_colorschemes
    NeoBundle 'altercation/vim-colors-solarized' "{{{
      let g:solarized_termcolors=256
      let g:solarized_termtrans=1
    "}}}
    NeoBundle 'nanotech/jellybeans.vim'
    NeoBundle 'tomasr/molokai'
    NeoBundle 'chriskempson/vim-tomorrow-theme'
    NeoBundle 'chriskempson/base16-vim'
    NeoBundle 'sjl/badwolf'
    NeoBundle 'endel/vim-github-colorscheme'
    NeoBundle 'zeis/vim-kolor' "{{{
      let g:kolor_underlined=1
    "}}}
  endif
"}}}

" Development in Progress {{{
if count(s:settings.plugin_groups, 'dev')

  " NeoBundle 'gcmt/wildfire.vim'
  " nmap <leader>m <Plug>(wildfire-quick-select)

  " NeoBundle 'thinca/vim-visualstar'


endif "}}}

" finish loading {{{
  if exists('g:dotvim_settings.disabled_plugins')
    for plugin in g:dotvim_settings.disabled_plugins
      exec 'NeoBundleDisable '.plugin
    endfor
  endif

  call neobundle#end()
  filetype plugin indent on
  syntax enable
  exec 'colorscheme '.s:settings.colorscheme

  NeoBundleCheck
"}}}
